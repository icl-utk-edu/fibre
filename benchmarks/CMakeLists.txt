#
# FFT Benchmarks
#

macro(hpfft_add_benchmark hpfft_benchmark)

    add_executable(${hpfft_benchmark} "${hpfft_benchmark}.c")
    target_link_libraries(${hpfft_benchmark} ${MPI_C_LIBRARIES})
    if (HPFFT_ENABLE_CUDA)
        target_link_libraries(${hpfft_benchmark} ${CUDA_LIBRARIES})
    endif()
    target_include_directories(${hpfft_benchmark} PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/../include/>)

    if (HPFFT_ENABLE_HEFFTE)
        target_link_libraries(${hpfft_benchmark} heffte)
    endif()
    if (HPFFT_ENABLE_FFTMPI)
        target_link_libraries(${hpfft_benchmark} fft3dmpi)
    endif()
    if (HPFFT_ENABLE_ACCFFT)
        # target_link_libraries(${hpfft_benchmark} accfft accfft_gpu  accfft_utils accfft_gpu_utils stdc++ fftw3_mpi fftw3_threads)
        target_link_libraries(${hpfft_benchmark} accfft fftw3_threads)
    endif()
    if (HPFFT_ENABLE_P3DFFT)
        target_link_libraries(${hpfft_benchmark} p3dfft.3 stdc++ fftw3 fftw3f)
    endif()
    if (HPFFT_ENABLE_FFTE)
        target_link_libraries(${hpfft_benchmark} fftempi ffte ${MPI_Fortran_LIBRARIES} ${CMAKE_Fortran_IMPLICIT_LINK_LIBRARIES})
    endif()
    if (HPFFT_ENABLE_SWFFT)
        target_link_libraries(${hpfft_benchmark} hpfft_swfft)
    endif()
    if (HPFFT_ENABLE_2DECOMP)
        target_link_libraries(${hpfft_benchmark} mpi_usempif08 mpi_usempi_ignore_tkr mpi_mpifh 2decomp_fft_iface 2decomp_fft fftw3f fftw3 gfortran)
    endif()
    if (HPFFT_ENABLE_NB3DFFT)
        target_link_libraries(${hpfft_benchmark} nb3dfft)
    endif()
    if (HPFFT_ENABLE_FFTW)
        target_link_libraries(${hpfft_benchmark} fftw3_mpi fftw3)
    endif()

endmacro()

macro(hpfft_add_mpi_test)
    cmake_parse_arguments(_hpfft "" "NAME;COMMAND;RANKS" "" ${ARGN} )
    add_test(${_hpfft_NAME} ${MPIEXEC_EXECUTABLE} ${MPIEXEC_NUMPROC_FLAG} ${_hpfft_RANKS} ${MPIEXEC_PREFLAGS} ${CMAKE_CURRENT_BINARY_DIR}/${_hpfft_COMMAND} ${MPIEXEC_POSTFLAGS})
    unset(_hpfft_NAME)
    unset(_hpfft_RANKS)
    unset(_hpfft_COMMAND)
endmacro()

if (HPFFT_ENABLE_FFTE)
    link_directories(${CMAKE_Fortran_IMPLICIT_LINK_DIRECTORIES})
endif() 

hpfft_add_benchmark(test3D_CPU_R2C)
hpfft_add_benchmark(test3D_CPU_C2C)
hpfft_add_benchmark(Bench_3D_C2C_CPU)

hpfft_add_benchmark(test3D_C2C)

if (HPFFT_ENABLE_CUDA)
    hpfft_add_benchmark(test3D_GPU_R2C)
    hpfft_add_benchmark(test3D_GPU_C2C)
endif() 
